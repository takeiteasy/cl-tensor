(in-package #:cl-tensor)

(defun computational-dtype (dtype)
  (when (dtype-storage-only-p dtype)
    (error "Dtype ~S is storage only; convert with ASTYPE before computing" dtype))
  dtype)

(defun coerce-value (value dtype)
  (coerce-scalar (find-dtype dtype) value))

(defmethod coerce-scalar ((descriptor dtype) value)
  (let ((dtype (dtype-name descriptor))
        (type (dtype-info-element-type descriptor)))
    (cond ((member dtype '(:f32 :f64))
           (unless (realp value) (error 'type-error :datum value :expected-type 'real))
           (coerce value type))
          ((member dtype '(:c32 :c64))
           (unless (numberp value) (error 'type-error :datum value :expected-type 'number))
           (let ((real-type (if (eq dtype :c32) 'single-float 'double-float)))
             (complex (coerce (realpart value) real-type)
                      (coerce (imagpart value) real-type))))
          (t
           (unless (typep value type)
             (error 'type-error :datum value :expected-type type))
           value))))

(defmacro define-foreign-access ()
  (let ((types '((:f32 :float) (:f64 :double)
                 (:s8 :int8) (:u8 :uint8) (:s16 :int16) (:u16 :uint16)
                 (:s32 :int32) (:u32 :uint32) (:s64 :int64) (:u64 :uint64)
                 (:c32 :float) (:c64 :double))))
    `(progn
       (defun foreign-ref (storage index)
         (let ((pointer (trivial-simd:vector-view-pointer storage)))
           (ecase (trivial-simd:vector-view-type storage)
             ,@(loop for (dtype foreign) in types
                     collect `(,dtype
                               ,(if (member dtype '(:c32 :c64))
                                    `(complex (cffi:mem-aref pointer ,foreign (* 2 index))
                                              (cffi:mem-aref pointer ,foreign (1+ (* 2 index))))
                                    `(cffi:mem-aref pointer ,foreign index)))))))
       (defun (setf foreign-ref) (value storage index)
         (let ((pointer (trivial-simd:vector-view-pointer storage)))
           (ecase (trivial-simd:vector-view-type storage)
             ,@(loop for (dtype foreign) in types
                     collect `(,dtype
                               ,(if (member dtype '(:c32 :c64))
                                    `(setf (cffi:mem-aref pointer ,foreign (* 2 index)) (realpart value)
                                           (cffi:mem-aref pointer ,foreign (1+ (* 2 index))) (imagpart value))
                                    `(setf (cffi:mem-aref pointer ,foreign index) value))))))
         value))))

(define-foreign-access)

(defmethod storage-ref ((storage t) index)
  (declare (ignore index))
  (error 'unsupported-storage-access :storage storage :access :read))

(defmethod (setf storage-ref) (value (storage t) index)
  (declare (ignore value index))
  (error 'unsupported-storage-access :storage storage :access :write))

(defmethod storage-ref ((storage vector) index)
  (aref storage index))

(defmethod storage-ref ((storage trivial-simd:vector-view) index)
  (foreign-ref storage index))

(defmethod (setf storage-ref) (value (storage vector) index)
  (setf (aref storage index) value))

(defmethod (setf storage-ref) (value (storage trivial-simd:vector-view) index)
  (setf (foreign-ref storage index) value))

(defun tref (tensor &rest indices)
  (storage-ref (tensor-storage tensor) (tensor-index tensor indices)))

(defun (setf tref) (value tensor &rest indices)
  (require-writable-storage tensor)
  (let ((index (tensor-index tensor indices)))
    (setf (storage-ref (tensor-storage tensor) index)
          (coerce-value value (tensor-dtype tensor))))
  value)

(defun call-with-offsets (shape layouts offsets function)
  (labels ((walk (axis)
             (if (= axis (length shape))
                 (apply function offsets)
                 (let ((count (aref shape axis)))
                   (dotimes (i count)
                     (walk (1+ axis))
                     (loop for tail on offsets for strides in layouts
                           do (incf (car tail) (aref strides axis))))
                   (loop for tail on offsets for strides in layouts
                         do (decf (car tail) (* count (aref strides axis))))))))
    (walk 0)))

(defmethod copy-storage-supported-p ((descriptor dtype) out)
  (storage-writable-p (tensor-storage out)))

(defmethod copy-storage! ((descriptor dtype) out input)
  (unless (and (eq (tensor-dtype out) (tensor-dtype input))
               (equalp (tensor-shape out) (tensor-shape input)))
    (error "Copy shape and dtype must match"))
  (unless (copy-storage-supported-p descriptor out)
    (error 'unsupported-storage-access :storage (tensor-storage out) :access :copy))
  (unless (storage-readable-p (tensor-storage input))
    (error 'unsupported-storage-access :storage (tensor-storage input) :access :read))
  (call-with-offsets (tensor-shape out)
                     (list (tensor-strides out) (tensor-strides input))
                     (list (tensor-offset out) (tensor-offset input))
                     (lambda (target source)
                       (setf (storage-ref (tensor-storage out) target)
                             (storage-ref (tensor-storage input) source))))
  out)

(defun copy-tensor (tensor)
  (let ((result (make-tensor (tensor-shape tensor) :dtype (tensor-dtype tensor))))
    (copy-storage! (find-dtype (tensor-dtype tensor)) result tensor)))

(defun require-writable-storage (tensor)
  (unless (storage-writable-p (tensor-storage tensor))
    (error 'unsupported-storage-access :storage (tensor-storage tensor) :access :write))
  tensor)
